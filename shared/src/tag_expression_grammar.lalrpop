use {
    crate::tag_expression::{TagExpression, Tag},
    std::sync::Arc
};

grammar;

pub TagExpression = {
    <a:TagExpression> "or" <b:And> => Box::new(TagExpression::Or(a, b)),
    And
}

And = {
    <a:And> "and" <b:Not> => Box::new(TagExpression::And(a, b)),
    Not
}

Not = {
    "not" <a:Term> => Box::new(TagExpression::Not(a)),
    Term
}

Term: Box<TagExpression> = {
    <category:Name> ":" <tag:Name> => Box::new(TagExpression::Tag(Tag {
      category: Some(Arc::from(category)),
      value: Arc::from(tag)
    })),
     <tag:Name> => Box::new(TagExpression::Tag(Tag {
       category: None,
       value: Arc::from(tag)
    })),
    "(" <s:TagExpression> ")" => s
}

Name = {
    r"[\w-]+"
}
